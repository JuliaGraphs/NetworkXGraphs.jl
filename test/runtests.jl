using Test
using CondaPkg
using Base.Threads: nthreads

CondaPkg.add("networkx")
CondaPkg.resolve()

using Graphs
using GraphsInterfaceChecker
using Interfaces
using NetworkX
using PythonCall

@testset "NetworkX.jl" begin
    nx = pyimport("networkx")

    @testset "Constructors and basic API" begin
        pyg = nx.Graph()
        pyg.add_edges_from([(10, 20), (20, 30), (30, 40)])
        gw = wrap_networkx(pyg)
        @test gw isa NetworkXGraph
        @test !is_directed(gw)
        @test nv(gw) == 4
        @test ne(gw) == 3
        @test collect(vertices(gw)) == [1, 2, 3, 4]
        @test has_vertex(gw, 1)
        @test !has_vertex(gw, 5)
        @test has_edge(gw, 1, 2)
        @test !has_edge(gw, 1, 4)
        @test sort(outneighbors(gw, 2)) == [1, 3]

        pydg = nx.DiGraph()
        pydg.add_edges_from([(1, 2), (2, 3), (4, 2)])
        dgw = wrap_networkx(pydg)
        @test dgw isa NetworkXDiGraph
        @test is_directed(dgw)
        @test nv(dgw) == 4
        @test ne(dgw) == 3
        @test outneighbors(dgw, 2) == [3]
        @test sort(inneighbors(dgw, 2)) == [1, 4]

        # Graphs.jl -> networkx -> wrapper roundtrip on basic structure.
        g = path_graph(5)
        gw2 = wrap_networkx(networkx_graph(g))
        @test nv(gw2) == 5
        @test ne(gw2) == 4
        @test has_edge(gw2, 2, 3)
    end


    @testset "Threaded isolation" begin
        if nthreads() > 1
            results = fill(false, 2 * nthreads())
            PythonCall.GIL.@unlock Threads.@threads for i in eachindex(results)
                pyg = PythonCall.GIL.@lock nx.Graph()
                PythonCall.GIL.@lock pyg.add_edges_from([(1, 2), (2, 3), (3, 4), (4, 5), (5, 5 + i)])
                gw = PythonCall.GIL.@lock wrap_networkx(pyg)
                ok = gw isa NetworkXGraph
                ok &= PythonCall.GIL.@lock nv(gw) == 6
                ok &= PythonCall.GIL.@lock ne(gw) == 5
                ok &= PythonCall.GIL.@lock has_edge(gw, 1, 2)
                ok &= !PythonCall.GIL.@lock is_directed(gw)

                pydg = PythonCall.GIL.@lock nx.DiGraph()
                PythonCall.GIL.@lock pydg.add_edges_from([(1, 2), (2, 3), (3, 1)])
                dgw = PythonCall.GIL.@lock wrap_networkx(pydg)
                ok &= dgw isa NetworkXDiGraph
                ok &= PythonCall.GIL.@lock is_directed(dgw)
                ok &= PythonCall.GIL.@lock outneighbors(dgw, 2) == [3]
                ok &= PythonCall.GIL.@lock inneighbors(dgw, 2) == [1]
                results[i] = ok
            end
            @test all(results)
        else
            @test true
        end
    end

    @testset "Interface checker" begin
        ug1 = nx.path_graph(5)
        ug2 = nx.complete_graph(4)
        dg1 = nx.DiGraph()
        dg1.add_edges_from([(1, 2), (2, 3), (3, 4)])
        dg2 = nx.complete_graph(4, create_using=nx.DiGraph())
        test_ugraphs = [wrap_networkx(ug1), wrap_networkx(ug2)]
        test_dgraphs = [wrap_networkx(dg1), wrap_networkx(dg2)]

        @test Interfaces.test(AbstractGraphInterface, NetworkXGraph, test_ugraphs)
        @test Interfaces.test(AbstractGraphInterface, NetworkXDiGraph, test_dgraphs)
    end
end
